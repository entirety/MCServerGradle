plugins {
    id 'java'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'net.minecrell.licenser' version '0.4.1'
}

sourceCompatibility = targetCompatibility = 1.8

compileJava {
    sourceCompatibility = targetCompatibility = 1.8
}

group 'io.entirety.mcservergradle'
version gitVersion()

def gitVersion() {
    // Check if git repo
    if (!grgit) return 'GITBORK'.toString()

    // Get most recent tag info
    def raw = null

    // Try find the latest git tag
    try {
        raw = grgit.describe(longDescr: true)
    } catch (Exception e) {
        e.printStackTrace()
    }

    // Set description
    def desc = (raw == null ? 'MAJOR.MINOR-PATCH-HASH' : raw).split('-') as List

    // Remove hash
    desc.removeLast()

    // Get parts from description
    def offset = desc.removeLast()
    def tag = desc.join('-')
    def branch = grgit.branch.current().name

    // Check branch
    if (branch in ['master', 'HEAD'])
        branch = null

    // Return the baked version string
    return "${tag}.${offset}${branch == null ? '' : ('-' + branch)}".toString()
}

license {
    header = file('HEADER')

    ext {
        name = 'MCServerGradle'
        year = 2018
        fullname = 'entirety'
    }

    exclude '**/*.properties'
}

wrapper {
    gradleVersion = '5.0'
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    jcenter()
    mavenCentral()

    maven {
        name = 'Forge'
        url = 'https://files.minecraftforge.net/maven'
    }
}

dependencies {
    implementation gradleApi()

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.+'
    implementation group: 'com.google.guava', name: 'guava', version: '27.0.+'
    implementation group: 'commons-io', name: 'commons-io', version: '2.+'
    implementation group: 'org.ajoberstar.grgit', name: 'grgit-core', version: '3.0.+'

    testImplementation gradleTestKit()

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.1.+'
    testCompileOnly group: 'junit', name: 'junit', version: '4.12'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.+'
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.1.+'
}

test {
    useJUnitPlatform()
}

task installGitHooks(type: Copy) {
    from "${projectDir}/.git-hooks"
    into "${projectDir}/.git/hooks"
}
